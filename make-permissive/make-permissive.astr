module make-permissive

imports
  libstratego-lib
  libstratego-xtc
  libstratego-sdf
  libstratego-sglr
  libstratego-gpp
  
  lib/sdf-desugar
  lib/multi-options
  lib/switch-options

  sdf-analysis
  sdf-heuristics
  water-sections
  insert-sections

overlays

  WATER_ON           = "water"
  OPEN_INSERTIONS_ON = "open-insertions"
  INSERTIONS_ON      = "insertions"

strategies // I/O

  main-make-permissive =
    xtc-io-wrap(
      options
    ,
      parse-xtc-file-report-errors(|<get-sdf-parse-table>, None());
      (?definition(_) <+ fatal-err(|"Please specify a packed .def file."));
      
      (oncetd(rename-main-module) <+ check-specified-main-module);

      id.init;

      add-permissive-sections;
      
      sdf-desugar;
      pp-sdf-string;
      print-to
    )
  
  options =
    SwitchOption(|
      OPEN_INSERTIONS_ON()
    , "off"
    , "Add recovery rules for opening braces"
    )
  + SwitchOption(|
      INSERTIONS_ON()
    , "on"
    , "Add insertion recovery rules"
   )
  + SwitchOption(|
      WATER_ON()
    , "on"
    , "Add water recovery rules"
    )
  + ArgOption(
      "-m"
    , <set-config> ("-m", <id>)
    , "-m INNAME           Specifies the input module name"
    )
  + ArgOption(
      "-mo"
    , <set-config> ("-mo", <id>)
    , "-mo OUTNAME         Specifies the output module name [INNAME-Permissive]"
    )
  
  check-specified-main-module =
    not(<get-config> "-m")
  <+
    <get-config> "-m";
    fatal-err(|"Specified main module not found")

  if-switch(|switch) =
    if-switch(!switch, ?x);
    where(!x)

strategies

  add-permissive-sections =
    definition(one(add-permissive-sections))

  add-permissive-sections:
    "module"#([name, x, sections]) ->
    "module"#([name, x, sections'])
    with
      added := id.all-permissive-sections;
      !sections;
      try(oncetd(
        \exports(e*) -> exports(conc-grammars(added, e*))\
      ));
      ?sections'

  rename-main-module:
    "module"#([unparameterized(name),  x, sections]) ->
    "module"#([unparameterized(name'), x, sections])
    where
      name.eq(|<get-config> "-m")
    with
      name' := <get-config> "-mo"
    <+
      name' := <conc-strings> (name, "-Permissive")

  rename-main-module:
    "module"#([parameterized(name,  p), x, sections]) ->
    "module"#([parameterized(name', p), x, sections])
    where
      name.eq(|<get-config> "-m")
    with
      name' := <get-config> "-mo"
    <+
      name' := <conc-strings> (name, "-Permissive")

  debug-sdf(m) =
    with(
      say(m);
      (is-list <+ ![<id>]);
      map(
        strip-annos;
        sdf-desugar;
        pp-sdf-string;
        debug(!"  ")
      )
    )

attributes

  def all-permissive-sections:
    grammar ->
    conc-grammars(
      water-sections
    , insert-sections
    )
    with
      water-sections  := grammar.water-sections;
      insert-sections := grammar.insert-sections
