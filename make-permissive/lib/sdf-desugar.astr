/**
 * sdf-desugar implements the following desugarings:
 *
 *   - remove double quotes in String literals: cons, lit
 *   - replace no-attrs by attrs([])
 *   - add quotes to generated quoted stings
 *
 * @author Martin Bravenboer <martin.bravenboer@gmail.com>
 * @author Lennart Kats <lennart add lclnet.nl>
 */
module lib/sdf-desugar

imports
  libstratego-aterm

strategies

  /**
   * Normalizes an SDF syntax definition to a grammar.
   *
   * Result is in desugared abstract syntax.
   */
  sdf-normalize =
    xtc-transform(!"sdf2table", !["-n", "-m", <get-config> "-m"]);
    xtc-transform(!"implodePT");
    sdf-desugar

strategies
  
  sdf-desugar =
      topdown(repeat(Desugar); try(DesugarOnce))

  Desugar :
    no-attrs() -> attrs([])

  Desugar :
    term(default(t)) -> term(t)

rules

  DesugarOnce =
    alltd(EnsureQuotes);
    term(implode-aterm)

  DesugarOnce :
    lit(s) -> lit(<un-double-quote> s)

  DesugarOnce :
    ci-lit(s) -> ci-lit(<un-single-quote> s)
  
  EnsureQuotes :
    quoted(s) -> quoted(<double-quote> s)
    where
      not(<un-double-quote> s)
