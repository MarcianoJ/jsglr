module sdf-analysis

imports
  sdf-injections

strategies

  Prod = prod(id, id, id)
  Sort = sort(id)

decorators 

  decorator context-free-args non-circular' graph-search(a) =
    a

  // TODO: Remove non-circular'; it's now in the standard library
  decorator non-circular'(a) =
    id.set-cached(a|EVAL_FAILED());
    a

attributes // Looking up productions

  def List lookup-all:
    sort(sort) -> id.lookup-all-sort(|sort)
  
  def cached(|id.signature.uninit) lookup-multiple-global lookup-all-sort(|sort):
    prod(_, sort(sort), _) -> id

attributes // Basic properties for productions and sorts

  def down current-sort:
    prod(_, sort(sort), _) -> sort
  
  def down current-cons:
    prod(_, _, attrs(a*)) -> cons
    where
      a*.one(?term(cons(cons)))
  
  def down current-prod:
    prod(_, _, _) -> id
  
  def Test down in-pattern:
    prod(pattern, _, _).pattern -> id

  def Test down in-context-free-section:
    context-free-syntax(_) -> id

  def Test down in-lexical-section:
    lexical-syntax(_) -> id

  def Test down in-kernel-section:
    syntax(_) -> id
  
  def direct-contained-sorts:
    prod(pattern, _, _) -> <collect-all(?sort(_))> pattern
    
  def direct-container-prods:
    prod(_, sort, _) -> sort.sort-direct-container-prods
    sort(_)          -> id.sort-direct-container-prods
  
  /**
   * Helper attribute that creates collections,
   * adding all productions to their 'direct-contained-sorts'.
   */
  def contributes-to(id.direct-contained-sorts, id.uninit) sort-direct-container-prods:
    prod(_, _, _) -> id
  
  def Test down in-iter-pattern:
    iter(_)             -> id
    iter-star(_)        -> id
    iter-star-sep(_, _) -> id
    iter-sep(_, _)      -> id

attributes // Start symbols

  def Test lookup-global is-start-symbol(|sort):
    context-free-start-symbols(s*) -> sort where s*.one(sort(id.eq(|sort)))
    lexical-start-symbols(s*)      -> sort where s*.one(sort(id.eq(|sort)))
    kernel-start-symbols(s*)       -> sort where s*.one(sort(id.eq(|sort)))

attributes // Graph structure properties

  /**
   * Tests if the current production directly or indirectly
   * is referenced by any production for the given sort.
   */
  def Type(Prod, Prod) graph-search is-contained-by(|sort):
    prod(_, _, _) -> id
    where
      id.direct-container-prods;
      (  one(id.current-sort.eq(|sort))
      <+ one(id.is-contained-by(|sort))
      )

  /**
   * Tests if the current production directly or indirectly
   * contains a reference to the given sort.
   */
  def Type(Prod, Prod) graph-search is-container-of(|sort):
    prod(_, _, _) -> id
    where
      id.direct-contained-sorts;
      (  one(id.eq(|sort(sort)))
      <+ one(
           id.lookup-all;
           one(id.is-container-of(|sort))
         )
      )
